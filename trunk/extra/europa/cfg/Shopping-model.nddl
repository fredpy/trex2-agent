#include "TREX.nddl"

// Classes for Locations (Home, SuperMarket, etc.), Products (Banana, etc.) and
// ProductLocations (Banana can be found at SuperMarket, for example)
/* 
class Location {
  string name;
  Location(string _name){
    name = _name;
  }
} */

/* 
class Product {
  string name;
  Product(string _name) {
    name = _name;
  }
} */

enum Location {Home, GroceryStore, ClothingStore};

enum Product {Apple, Milk, Shirt};

class ProductLocation {
  Location location;
  Product product;

  ProductLocation(Location _location, Product _product){
    location = _location;
    product = _product;
  }
}

class Path {
   Location from;
   Location to;
   
   Path(Location _from, Location _to){
     from = _from;
     to = _to;
   }
}

// Use built-in Timeline functionality to enforce that an agent:
// a) Can't be at more than one place at a time.
// b) Can't Go more than one place at a time.
// c) Can't Go somewhere and be At somewhere at the same time.
class AgentLocation extends AgentTimeline{

  AgentLocation(Mode _mode){
    super(_mode);
  }

  predicate At {
    Location loc;
  }

  predicate Go {
    Location from;
    Location to;
  }
}

// Used to as object so that the agent can own multiple products
class AgentStorage {

  predicate Own {
    Product product;
  }
}

// In addition to a timeline, the agent can buy things.  Note that Buy can
// (must, in fact) be simultaneous with an At token and the Buy predicate
// therefore cannot be defined as part of the AgentTimeline.
class Agent extends AgentTimeline {
  AgentStorage backpack;

  Agent(Mode _mode) {
    super(_mode, true);
    backpack = new AgentStorage();
  }

  action Go {
    Location from;
    Location to;
  }

  action Buy {
    Product product;
  }

  action Eat {
    Product product;
    Location at;
  }

  predicate Want {
    Product product;
  }

}

// Define the rules for AgentLocation

AgentLocation::At {
  met_by(Go prevGo);
  eq(loc, prevGo.to);
  meets(Go nextGo);
  eq(loc, nextGo.from);

}

AgentLocation::Go {
  met_by(At originToken);
  meets(At destinationToken);

  Path paths;
  eq(originToken.loc, paths.from);
  eq(destinationToken.loc, paths.to);

  eq(from, paths.from);
  eq(to, paths.to);

}

// Define the rules for Agent

Agent::Go {
  met_by(condition AgentLocation.At origin);
  eq(from, origin.loc);

  equals(condition AgentLocation.Go going);
  eq(going.from, from);
  eq(going.to, to);

  meets(effect AgentLocation.At destination);
  eq(to, destination.loc);

}

Agent::Buy {

  // initialized to all locations
  ProductLocation possibleStores;

  // limit possibleStores variables to ones that provide what we need to buy
  eq(product, possibleStores.product);

  // We must be At a location during our Buy, and that location must have the
  // product we want available:
  contained_by(condition AgentLocation.At currLocation);
  eq(currLocation.loc, possibleStores.location);
  
  meets(effect Want purchase);
  eq(purchase.product,product);

  // A Buy takes 10 time units
  eq(10, duration);
}

Agent::Eat {
  contained_by(condition AgentLocation.At location);
  eq(location.loc, at);

  ends(effect object.backpack.Own food2);
  eq(food2.product, product);

  eq(5, duration);

}

Agent::Want {
  eq(1, duration);
  starts(effect object.backpack.Own backpack);
  eq(backpack.product, product);
}
